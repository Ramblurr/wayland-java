if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'signing'

    sourceCompatibility = JavaVersion.VERSION_1_6

    group = 'org.freedesktop'
    version = '1.1.0'

    repositories {
        mavenCentral()
    }
}

def exportedProjects = [
        ":stubs",
        ":wayland",
]

task alljavadoc(type: Javadoc) {
    source exportedProjects.collect { project(it).sourceSets.main.allJava }
    classpath = files(exportedProjects.collect { project(it).sourceSets.main.compileClasspath })
    destinationDir = file("${buildDir}/docs/javadoc")
}

alljavadoc.dependsOn(":wayland:compileJava")

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jar

    archives sourcesJar
    archives javadocJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'wayland-java-bindings'
                packaging 'jar'
                description 'The Wayland protocol Java-language Binding'
                url 'https://github.com/Zubnix/wayland-java-bindings'

                scm {
                    url 'scm:git@github.com:Zubnix/wayland-java-bindings.git'
                    connection 'scm:git@github.com:Zubnix/wayland-java-bindings.git'
                    developerConnection 'scm:git@github.com:Zubnix/wayland-java-bindings.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'Zubnix'
                        name 'Erik De Rijcke'
                    }
                }
            }
        }
    }
}
